SUBSCRIPTION_ID := "98fd3145-0fad-4970-9164-08dfe291b32f"
VM_NAME := "dotfiles-vm"

export UV_PRERELEASE := "allow"

default:
    @just --list

run:
    @just setup
    @just create
    echo "Waiting 20 seconds for the VM to be ready..."
    @sleep 20
    @just apply
    @just clean

install:
    uv tool install --python=3.11 --prerelease=allow --with=pip azure-cli
    az extension add --name ssh

setup: install
    #!/usr/bin/env zsh
    if ! az account show &>/dev/null; then
        az login
    fi
    az account set --subscription {{SUBSCRIPTION_ID}}

create:
    #!/usr/bin/env zsh
    mise use terraform
    terraform init -upgrade
    terraform plan -out main.tfplan
    terraform apply -auto-approve main.tfplan

    resource_group_name=$(terraform output -raw resource_group_name)
    az vm list \
        --resource-group $resource_group_name \
        --query "[].{\"VM Name\":name}" \
        -o table

ssh: setup
    #!/usr/bin/env zsh
    resource_group_name=$(terraform output -raw resource_group_name)
    az vm user update \
        --resource-group $resource_group_name \
        --name {{VM_NAME}} \
        --username kenza \
        --ssh-key-value "$(ssh-add -L | head -n 1)"

destroy:
    terraform plan -destroy -out main.destroy.tfplan
    terraform apply -auto-approve main.destroy.tfplan

remake: destroy create

connect: ssh
    #!/usr/bin/env bash
    resource_group=$(terraform output -raw resource_group_name)
    az ssh vm \
        --resource-group $resource_group \
        --name {{VM_NAME}} \
        --local-user kenza

apply: ssh
    #!/usr/bin/env bash
    resource_group=$(terraform output -raw resource_group_name)
    az ssh vm \
        --resource-group $resource_group \
        --name {{VM_NAME}} \
        --local-user kenza \
            -- \
            -t 'sh -c "$(curl -fsLS get.chezmoi.io)" -- init --apply kenza-bouzid; zsh'
